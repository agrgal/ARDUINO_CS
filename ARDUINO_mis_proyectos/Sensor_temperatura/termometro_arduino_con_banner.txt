#include <stdio.h>
#include <math.h>
// Incluyo la biblioteca para el display
#include <LiquidCrystal.h>

int analogPin=0;   // Which pin will be the input of the Voltage-Divider

float Vin=5.0;     // [V]        Supply voltage in the Voltage-Divider
float Raux=10000;  // [ohm]      Secondary resistor in the Voltage-Divider
float R0=14750;    // [ohm]      NTC nominal value at 0ºC
float T0=273.15;   // [K] (25ºC)

float Vout=0.0;    // [V]        Voltage given by the Voltage-Divider
float Rout=0.0;    // [ohm]      Current NTC resistance

float T1=273.15;      // [K]        Temperature at first testing point
float T2=293.55;      // [K]        Temperature at second testing point
float RT1=14750;   // [ohms]     Resistance at 273K (0ºC)
float RT2=6620;    // [ohms]     Resistance at 373K (100ºC)

float beta=0.0;    // [K]        Beta parameter
float Rinf=0.0;    // [ohm]      Rinf parameter
float TempK=0.0;   // [K]        Temperature output in Kelvin
float TempC=0.0;   // [ºC]       Temperature output in Celsius

char miStr[]="                Bienvenidas.Bienvenidos. TERMOMETRO ARDUINO DEL IES SERITIUM - I Feria de la Ciencia de Jerez.                ";

int i;
int inicio = 0;
int final = 0;
int numcaracteres = 16;

LiquidCrystal lcd(12, 11, 5, 4, 3, 2); // declaracion pines lcd

byte cerito[8] = {
  B01110,
  B01010,
  B01110,
  B00000,
  B00000,
  B00000,
  B00000,
};

void setup() {
  lcd.createChar(0, cerito); // caracter nmero cero, el "cerito" de los grados centgrados
  // Establece numero de columnas y filas del display
  lcd.begin(8, 2);
  // Mensaje del display
  // lcd.print(miStr);
  final = sizeof(miStr);
  
  //Serial port configuration
  Serial.begin(9600);

  //Input pin configuration
  pinMode(analogPin, INPUT);

  //Global parameters calculation
  beta=(log(RT1/RT2))/((1/T1)-(1/T2));
  Rinf=R0*exp(-beta/T0);
}

void loop()
{
  //Current NTC resistance value calculation (through Vout)
  Vout=Vin*((float)(analogRead(analogPin))/1024.0);
  Rout=(Raux*Vout/(Vin-Vout));

  //Temperature calculation
  TempK=(beta/log(Rout/Rinf));
  TempC=TempK-273.15;

  //Data population
  Serial.print(TempC);
  Serial.print(" ºC (");
  Serial.print(TempK);
  Serial.println(" K)");
  
  // Muestra el mensaje en el display
  for (i=0;i<15;i++) {
     lcd.setCursor(i, 0);
     lcd.print(miStr[inicio+i]);
  }
 
  // Pone el cursor en la segunda lnea
  lcd.setCursor(0, 1);

  // Imprime la temperatura en grados Centgrados
  lcd.print((int) TempC); // parte entera
  lcd.print(".");
  lcd.print(((int) (TempC*10)) - ((int) TempC)*10); // parte decimal: uno solo
  lcd.write(byte(0)); // imprime en pantalla el "cerito"
  lcd.print("C / ");
  // Imprime la temperatura en Kelvin
  lcd.print((int) TempK);  // parte entera
  lcd.print(".");
  lcd.print(((int) (TempK*10)) - ((int) TempK)*10); // parte decimal: uno solo
  lcd.print(" K");

  delay(300);
  // despues establece 
  inicio=(inicio+1)*(inicio<final-numcaracteres); 
}
